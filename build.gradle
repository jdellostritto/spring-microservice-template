plugins {
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.4.0'
    id "org.sonarqube" version "7.1.0.6387"
    id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
}
group = 'com.jdellostritto.microservice'
version = '0.0.1-SNAPSHOT'
java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agent
}

repositories {
    mavenCentral()
}


dependencies {
    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:2.13.3"

    /* actuator */
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    /* webflux  */
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    /* Logging  */
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    /* OpenTelemetry */
    runtimeOnly group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-micrometer-1.5', version: '2.14.0-alpha'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.12.5'

    /* AOP / AspectJ for metrics interception */
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    /* Springdoc  */
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.6.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-api:2.6.0'

    /* lombok   */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

     /* Logging */
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    /* test     */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

task copyAgent(type: Copy) {
    from configurations.agent {
        rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
    }
    into "${buildDir}/agent"
}

jib {
    from {
        //Option 1: Alpine base image from GHCR (lightweight, recommended for production)
        image = "ghcr.io/jdellostritto/docker-jdk-base/jdk-alpine:latest"

        //Option 2: Ubuntu base image from GHCR (broader package availability)
        //image = "ghcr.io/jdellostritto/docker-jdk-base/jdk-ubuntu:latest"
    }
    to {
        image = "ghcr.io/jdellostritto/spring-microservice-template"
        tags = ["latest", "master"]
    }
    container {
        appRoot = '/usr/app'
        user = 'appuser' //REQUIRED if using base-zulu-alpine
        jvmFlags = ['-Xms360m', '-Xmx360m', '-javaagent:/usr/app/opentelemetry-javaagent.jar']
        ports = ['8700','9001']
        workingDirectory = '/usr/app'
        volumes = ['/usr/app/logs'] //REQUIRED if running the default profile.
        files {
            from "${buildDir}/agent"
            into '/usr/app'
        }
    }
    allowInsecureRegistries = false
}

testing {
    suites {
        test {
            useJUnitJupiter()

        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                /* webflux  */
                implementation 'org.springframework.boot:spring-boot-starter-webflux'
                implementation 'org.springframework.boot:spring-boot-starter-test'
            }
        }
    }
}
test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test// tests are required to run before generating the report
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')

    }
}

sonar {
    properties {
        
        // Project identification
        property "sonar.projectKey", "jdellostritto_spring-microservice-template"
        property 'sonar.projectName', 'Spring Microservice Template'
        property "sonar.organization", "jdellostritto"
        
        // Source encoding
        property 'sonar.sourceEncoding', 'UTF-8'
        
        // Java version
        property 'sonar.java.source', '21'
        property 'sonar.java.target', '21'
        
        // Test results
        property 'sonar.junit.reportPaths', 'build/test-results/test/*.xml'
        
        // Code coverage with JaCoCo
        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        
        // Exclusions (optional)
        property 'sonar.exclusions', '**/generated/**,**/config/**'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks['sonarqube'].dependsOn test

// This adds HARNESS_JAVA_AGENT to the testing command if it's
// provided through the command line.
// Local builds will still remain same as it only adds if the
// parameter is provided.
tasks.withType(Test) {
  if(System.getProperty("HARNESS_JAVA_AGENT")) {
    jvmArgs += [System.getProperty("HARNESS_JAVA_AGENT")]
  }
}

// This makes sure that any test tasks for subprojects don't
// fail in case the test filter does not match.
gradle.projectsEvaluated {
        tasks.withType(Test) {
            filter {
                setFailOnNoMatchingTests(false)
            }
        }
}

// OpenAPI Gradle Plugin Configuration
openApi {
    apiDocsUrl = "http://localhost:8700/v3/api-docs.yaml"
    outputDir = file("$buildDir/openapi")
    outputFileName = "openapi.yaml"
    waitTimeInSeconds = 30
}
