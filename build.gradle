plugins {
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.4.0'
    id "org.sonarqube" version "7.1.0.6387"
}
group = 'com.jdellostritto.microservice'
version = '0.0.1-SNAPSHOT'
java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {
    /* actuator */
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    /* webflux  */
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    /* Logging  */
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    /* Springdoc  */
    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.5.13'
    implementation 'org.springdoc:springdoc-openapi-webflux-core:1.5.13'

    /* lombok   */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

     /* Logging */
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    /* test     */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}


jib {
    from {
        //Option 1: Alpine base image from GHCR (lightweight, recommended for production)
        image = "ghcr.io/jdellostritto/docker-jdk-base/jdk-alpine:latest"

        //Option 2: Ubuntu base image from GHCR (broader package availability)
        //image = "ghcr.io/jdellostritto/docker-jdk-base/jdk-ubuntu:latest"

        //Option 3: Local testing with Azul Zulu from Docker Hub
        //image = "azul/zulu-openjdk-alpine:11"

    }
    to {
        image = "ghcr.io/jdellostritto/spring-microservice-template"
        tags = ["latest", "master"]
    }
    container {
        appRoot = '/usr/app'
        user = 'appuser' //REQUIRED if using base-zulu-alpine
        jvmFlags = ['-Xms360m', '-Xmx360m']
        ports = ['8700','9001']
        workingDirectory = '/usr/app'
        volumes = ['/usr/app/logs'] //REQUIRED if running the default profile.
    }
    allowInsecureRegistries = false
}

testing {
    suites {
        test {
            useJUnitJupiter()

        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                /* webflux  */
                implementation 'org.springframework.boot:spring-boot-starter-webflux'
                implementation 'org.springframework.boot:spring-boot-starter-test'
            }
        }
    }
}
test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test// tests are required to run before generating the report
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')

    }
}

sonar {
    properties {
        
        // Project identification
        property "sonar.projectKey", "jdellostritto_spring-microservice-template"
        property 'sonar.projectName', 'Spring Microservice Template'
        property "sonar.organization", "jdellostritto"
        
        // Source encoding
        property 'sonar.sourceEncoding', 'UTF-8'
        
        // Java version
        property 'sonar.java.source', '21'
        property 'sonar.java.target', '21'
        
        // Test results
        property 'sonar.junit.reportPaths', 'build/test-results/test/*.xml'
        
        // Code coverage with JaCoCo
        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        
        // Exclusions (optional)
        property 'sonar.exclusions', '**/generated/**,**/config/**'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks['sonarqube'].dependsOn test

// This adds HARNESS_JAVA_AGENT to the testing command if it's
// provided through the command line.
// Local builds will still remain same as it only adds if the
// parameter is provided.
tasks.withType(Test) {
  if(System.getProperty("HARNESS_JAVA_AGENT")) {
    jvmArgs += [System.getProperty("HARNESS_JAVA_AGENT")]
  }
}

// This makes sure that any test tasks for subprojects don't
// fail in case the test filter does not match.
gradle.projectsEvaluated {
        tasks.withType(Test) {
            filter {
                setFailOnNoMatchingTests(false)
            }
        }
}

// Javadoc configuration
javadoc {
	destinationDir = file("${buildDir}/docs/javadoc")
	options {
		encoding = 'UTF-8'
		docEncoding = 'UTF-8'
		charSet = 'UTF-8'
	}
}

// OpenAPI Specification generation
task resolveOpenApi {
	description = 'Generate OpenAPI specification in YAML format'
	doLast {
		// Start the application briefly to generate the OpenAPI spec
		def process = ["${System.getProperty('java.home')}/bin/java",
			'-jar', tasks.bootJar.outputs.files.singleFile.absolutePath,
			'--server.port=0'].execute()
		
		Thread.sleep(5000) // Wait for app to start
		
		try {
			// Try to fetch the OpenAPI spec
			new URL('http://localhost:8080/v3/api-docs.yaml')
				.withInputStream { inputStream ->
					def outputDir = new File("${buildDir}/openapi")
					outputDir.mkdirs()
					new File(outputDir, 'openapi.yaml').withOutputStream { outputStream ->
						outputStream << inputStream
					}
				}
			println "✅ OpenAPI spec generated: ${buildDir}/openapi/openapi.yaml"
		} catch (Exception e) {
			println "⚠️  Could not fetch OpenAPI spec: ${e.message}"
		} finally {
			process.destroy()
		}
	}
}
